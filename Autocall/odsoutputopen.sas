%* MACRO %ODSOutputOpen;
%* Created: 01-Sep-2015;
%* Modified: 29-Nov-2018;
%* Author: Daniel Mastropietro;
%* Open an ODS output file for writing;
%* ALL paths should be UNQUOTED;
%* OTHER MACROS USED: %getwd;
%* SEE ALSO: %ODSOutputClose;
%MACRO ODSOutputOpen(odsfile, odspath=, odsfiletype=pdf, macro=, log=0) / des="Opens an ODS output file for writing";
%if %quote(&odsfile) ~= and %quote(&odsfiletype) ~= %then %do;

	%* OUTPUT PATH;
	%if %quote(&odspath) = %then
		%* Use the working directory as output path;
		%let odspath = %getwd;

	%* OUTPUT TYPE;
	%if %upcase(%quote(&odsfiletype)) = HTML %then %do;
		%* This distinction of HTML output is necessary because this is the only format that accepts the PATH= option...;
		%* The reason being that the HTML output stores graphs in separate files (e.g. PNG files that are linked to the HTML output);
		%* Note that the names of those separate files (e.g. PNG) use the IMAGENAME= option of the ODS GRAPHICS statement
		%* as root name and are numbered with an index to avoid override (e.g. test.png, test1.png, etc.), but these numbers
		%* may NOT be consecutive, if another HTML destination is open (like the DEFAULT HTML destination!, and therefore
		%* depending on the number of files containing plots generated by the plotting process, the numbers may vary.
		%* For instance, if 3 graphs are generated by the plotting process, the file names will be:
		%* test1.png, test3.png, test5.png
		%* because each plot is sent both to the default HTML destination and the HTML destination
		%* that is opened by the user when calling this macro.
		%* I DO NOT KNOW HOW TO CLOSE THE DEFAULT HTML DESTINATION (HTML CLOSE DOES NOT WORK!);
		%* The default format of the files is PNG and can be changed using the OUTPUTFMT= option of the ODS GRAPHICS statement;
		ods &odsfiletype path="&odspath" file="&odsfile" style=statistical;
	%end;
	%else %do;
		ods &odsfiletype file="&odspath/&odsfile" style=statistical;
	%end;

	%if &log %then %do;
		%if %quote(&macro) ~= %then
			%put %upcase(&macro): %upcase(&odsfiletype) file "&odsfile" opened for output by ODS.;
		%else
			%put %upcase(&odsfiletype) file "&odsfile" opened for output by ODS.;
		%put;
	%end;
%end;
%MEND ODSOutputOpen;
